use 5.12.0;
use strict;
use warnings;

use File::Compare ();
use File::Spec ();
use File::Temp ();

use constant NUM_FILES => 8;
use constant MB_PER_FILE => 8;

MAIN(@ARGV);

sub MAIN {
    my ($num_MB) = @_;

    my @files = do {
        my $dir_template = File::Spec->catdir(
            File::Spec->tmpdir, 'file-compare-XXXXXXXXXX'
        );
        my $dir = File::Temp::tempdir($dir_template, CLEANUP => 1);
        my $bytes_per_file = MB_PER_FILE * 1024*1024;

        map {
            my (undef, $filename) = File::Temp::tempfile(DIR => $dir, OPEN => 0);
            system("head -c $bytes_per_file /dev/random > $filename");
            die "bad file size: " . (-s $filename) . " bytes"
                unless -s $filename == $bytes_per_file;
            $filename;
        } 1 .. NUM_FILES;
    };

    for (1 .. $num_MB/MB_PER_FILE) {
        my $file1 = shift @files;
        my $file2 = $files[ rand @files ];
        File::Compare::compare($file1, $file2);
        push @files, $file1;
    }
}

# end
